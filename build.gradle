// First, apply the publishing plugin
buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.6"
        classpath "gradle.plugin.org.mockito:mockito-release-tools:0.8.13"
    }
}

apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'checkstyle'
apply plugin: 'codenarc'
apply plugin: 'org.mockito.mockito-release-tools.gradle-plugin-releasing'

apply from: 'gradle/precommit.gradle'
apply from: 'gradle/release.gradle'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories { jcenter() }

group = 'gradle.plugin.org.mockito'

dependencies {
    compile gradleApi()

    compile "com.github.cliftonlabs:json-simple:2.1.2"
    compile 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'

    testCompile("org.spockframework:spock-core:0.7-groovy-2.0") {
        exclude module: "groovy-all"
    }
    testCompile "cglib:cglib-nodep:2.2.2"
    testCompile "commons-lang:commons-lang:2.4"
    testCompile gradleTestKit()
}

pluginBundle {
    website = 'http://mockito.org/'
    vcsUrl = 'https://github.com/mockito/mockito-release-tools'
    description = 'Release tools and Gradle plugins that automate Mockito continuous delivery.'
    tags = ['mockito', 'continuous delivery', 'release']

    plugins {
        //TODO update plugins
        releaseNotesPlugin {
            id = 'org.mockito.release-notes'
            displayName = 'Mockito automated release notes generation plugin'
        }

        autoVersioningPlugin {
            id = 'org.mockito.release-tools.auto-versioning'
            displayName = 'Mockito automated versioning plugin'
            tags = ['mockito', 'continuous delivery', 'release', 'versioning', 'auto-versioning', 'semantic-versioning']
        }

        gradlePluginReleasingPlugin {
            id = 'org.mockito.mockito-release-tools.gradle-plugin-releasing'
            displayName = 'Mockito automated versioning and Travis CI integration'
            tags = ['mockito', 'continuous delivery', 'release', 'versioning', 'auto-versioning', 'semantic-versioning', 'travis']
        }

        releaseNeededPlugin {
            id = 'org.mockito.mockito-release-tools.release-needed'
            displayName = 'Mockito plugin for checking if release is needed'
            tags = ['mockito', 'continuous delivery', 'release']
        }
    }

    mavenCoordinates {
        groupId = 'gradle.plugin.org.mockito'
        artifactId = "mockito-release-tools"
    }
}

task releaseNeeded {
    ext.needed = true
    doLast {
        def pullRequest = System.getenv('TRAVIS_PULL_REQUEST')?.matches("\\d+")
        def onMaster = "master" == System.getenv('TRAVIS_BRANCH')
        ext.needed = !pullRequest && onMaster
        if (needed) {
            logger.lifecycle("{} - Release to Gradle Plugin Portal is needed.", path)
        } else {
            logger.lifecycle("{} - Current branch is not 'master' or we're building pull request.\n" +
                    "  Release to Gradle Plugin Portal is not needed.", path)
        }
    }
}

//TODO ww add -Preleasing.dryRun=false to commandLine when releaseNeeded takes into account [ci skipRelease] in commit msg
task travisRelease {
    dependsOn releaseNeeded
    onlyIf { releaseNeeded.needed }
    doLast {
        logger.lifecycle("{} - Publishing to Gradle Plugin Portal...", path)
        exec {
            commandLine "./gradlew", "publishPlugins", "bumpVersionAndPush",
                    "-Pgradle.publish.key=${System.getenv('GRADLE_PUBLISH_KEY')}",
                    "-Pgradle.publish.secret=${System.getenv('GRADLE_PUBLISH_SECRET')}"
        }
    }
}

task fastInstall { Task t ->
    description = "Fast installation for quick local testing."
    t.dependsOn install
    gradle.taskGraph.whenReady {
        if (it.hasTask(t)) {
            //we don't need them for local testing so lets disable for faster execution
            tasks.javadoc.enabled = false
            tasks.groovydoc.enabled = false
        }
    }
}